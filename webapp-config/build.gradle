bootJar.enabled = true
jar.enabled = false

dependencies {
    implementation project(':web-adapter')

    implementation project(':api-in')
    implementation project(':application')
    implementation project(':api-out')

    implementation project(':statistics-persistence-adapter')
    implementation project(':jira-rest-adapter')

    implementation project(':statistics-jpa')
    implementation project(':jira-rest-client')

    implementation project(':support')
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
        resources.srcDir file('src/webapp')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    afterTest { desc, result ->
        logger.quiet "Executing integration test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.named("bootJar") {
    mainClass = 'kr.co.mz.ApplicationInitializr'
}

tasks.named("bootRun") {
    systemProperties = System.properties
}